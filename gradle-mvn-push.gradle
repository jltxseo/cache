apply plugin: 'maven'
apply plugin: 'signing'

def initConfig(){
    Properties properties = new Properties()
    File propertyFile = new File(rootDir.getAbsolutePath()+ "/mvn-config.properties")
    properties.load(propertyFile.newDataInputStream())

    gradle.ext.releaseUrl =properties.getProperty('RELEASE_REPOSITORY_URL')
    gradle.ext.snapshotUrl =properties.getProperty('SNAPSHOT_REPOSITORY_URL')
    gradle.ext.userName =properties.getProperty('NEXUS_USERNAME')
    gradle.ext.password =properties.getProperty('NEXUS_PASSWORD')
}

def isReleaseBuild() {
    return PROJ_VERSION.contains("SNAPSHOT") == false
}

def getReleaseRepositoryUrl() {
    return gradle.ext.releaseUrl
}

def getSnapshotRepositoryUrl() {
    return gradle.ext.snapshotUrl
}

def getRepositoryUsername() {
    return gradle.ext.userName
}

def getRepositoryPassword() {
    return gradle.ext.password
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                initConfig()
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                pom.groupId = PROJ_GROUP
                pom.artifactId = PROJ_ARTIFACTID
                pom.version = PROJ_VERSION

                if (isReleaseBuild() == false) {
                    snapshotRepository(url: getSnapshotRepositoryUrl()) {
                        authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                    }
                } else {
                    repository(url: getReleaseRepositoryUrl()) {
                        authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                    }
                }

                pom.project {
                    name PROJ_POM_NAME
                    packaging POM_PACKAGING
                    description PROJ_DESCRIPTION
                    url PROJ_WEBSITEURL

                    scm {
                        url PROJ_VCSURL
                        connection DEVELOPER_EMAIL
                        developerConnection DEVELOPER_EMAIL
                    }

                    developers {
                        developer {
                            id DEVELOPER_ID
                            name DEVELOPER_NAME
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    task androidJavadocs(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }

    task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
        classifier = 'javadoc'
        from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.sourceFiles
    }

    artifacts {
//        archives androidSourcesJar
//        archives androidJavadocsJar
    }
}
